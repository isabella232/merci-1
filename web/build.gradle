/*
 * Copyright 2018 Medallia, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at

 *     http://www.apache.org/licenses/LICENSE-2.0

 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: 'application'
mainClassName = "com.medallia.merci.web.JettyRunner"

ext {
    jdkVersion = JavaVersion.current().isJava8() ? 'jdk8' : 'jdk9'
}

dependencies {

    compile project(':core')

    // Common libraries
    compile 'commons-cli:commons-cli:20040117.000000'
    compile 'commons-configuration:commons-configuration:1.10'
    compile 'com.google.code.findbugs:jsr305:3.0.1'

    // Servlet API
    compile 'javax.servlet:javax.servlet-api:3.1.0'

    // Jersey
    compile 'org.glassfish.jersey.containers:jersey-container-servlet:2.25.1'

    // Jackson for JSON and YAML
    compile 'com.fasterxml.jackson.core:jackson-core:2.9.4'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.9.4'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.9.4'
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.9.4'
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.9.4'
    compile 'com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:2.9.4'

    // Jetty
    compile 'org.eclipse.jetty:jetty-server:9.4.6.v20170531'
    compile 'org.eclipse.jetty:jetty-webapp:9.4.6.v20170531'

    // SL4J and Logback for logging
    compile 'org.slf4j:slf4j-api:1.7.25'
    compile 'ch.qos.logback:logback-classic:1.2.3'
    compile 'ch.qos.logback:logback-access:1.2.3'
    compile 'ch.qos.logback:logback-core:1.2.3'

    // JUnit and Mockito for unit tests
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
}

task sourceJar( type: Jar, dependsOn: classes ) {
    classifier "sources"
    from sourceSets.main.allJava
}

artifacts {
    archives jar
    archives sourceJar
    archives distTar
    archives distZip
}

signing {
    sign configurations.archives
}

def sonatypeUsername = project.hasProperty('sonatypeUsername') ? sonatypeUsername : System.getenv('sonatypeUsername')
def sonatypePassword = project.hasProperty('sonatypePassword') ? sonatypePassword : System.getenv('sonatypePassword')

publishing {
    publications {
        mavenWeb(MavenPublication) {
            pom.withXml {
                def root = asNode()

                root.dependencies.removeAll { dep ->
                    dep.scope == "test"
                }

                root.children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST

                    description 'Framework for Feature Flags and Runtime Configuration'
                    name 'Merci'
                    url 'https://github.com/medallia/merci'
                    organization {
                        name 'com.github.medallia'
                        url 'https://github.com/medallia'
                    }
                    issueManagement {
                        system 'GitHub'
                        url 'https://github.com/medallia/merci/issues'
                    }
                    licenses {
                        license {
                            name 'Apache License 2.0'
                            url 'https://github.com/medallia/merci/blob/master/LICENSE'
                            distribution 'repo'
                        }
                    }
                    scm {
                        url 'https://github.com/medallia/merci'
                        connection 'scm:git:git://github.com/medallia/merci.git'
                        developerConnection 'scm:git:ssh://git@github.com:medallia/merci.git'
                    }
                    developers {
                        developer {
                            name 'Mario H. Lichtsinn'
                        }
                    }
                }
            }

            groupId 'com.medallia.merci'
            artifactId 'merci-web'
            version version
            from components.java

            pom.withXml {
                def pomFile = file("${project.buildDir}/generated-pom.xml")
                writeTo(pomFile)
                def pomAscFile = signing.sign(pomFile).signatureFiles[0]
                artifact(pomAscFile) {
                    classifier = null
                    extension = 'pom.asc'
                }
            }

            artifact(sourceJar) {
                classifier = 'sources'
            }

            artifact (distTar) {
                classifier = null
            }

            artifact (distZip) {
                classifier = null
            }

            project.tasks.signArchives.signatureFiles.each {
                artifact(it) {
                    def matcher = it.file =~ /-?(?<classifier>sources|javadoc)?\.(?<extension>jar|tar|zip)\.asc$/
                    if (matcher.find()) {
                        classifier = matcher.group('classifier')
                        extension = matcher.group('extension') + '.asc'
                        println classifier
                        println extension
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }

    model {
        tasks.generatePomFileForMavenWebPublication {
            destination = file("$buildDir/generated-pom.xml")
        }
        tasks.publishMavenWebPublicationToMavenLocal {
            dependsOn project.tasks.signArchives
        }
        tasks.publishMavenWebPublicationToMavenRepository {
            dependsOn project.tasks.signArchives
        }
    }
}
